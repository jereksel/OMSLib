buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def libraryVersion = System.getenv("TRAVIS_TAG") ?: "a"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: "b"
    key = System.getenv('BINTRAY_KEY') ?: "c"
    publish = true
    pkg {
        repo = 'maven'
        name = "OMSLib"
        publish = true
        publicDownloadNumbers = true
        version {
            name = "$libraryVersion"
            vcsTag = "$libraryVersion"
        }
    }
    publications = ['MyPublication']
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.jereksel'
            artifactId 'omslib'
            version libraryVersion
            artifact "${project.buildDir}/outputs/aar/${project.getName()}-release.aar"
            artifact sourcesJar
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
